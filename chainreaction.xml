<svg
	xmlns="http://www.w3.org/2000/svg"
	xmlns:xlink="http://www.w3.org/1999/xlink"
	width="100%" height="100%"
	viewBox="0 0 32 32"
>
	<title>Chainreaction</title>
	<script type="module" xlink:href="chainreaction.js"></script>
	<script id="xquery-module" type="application/xquery">
<![CDATA[
module namespace chain="http://mansoft.nl/chain";

declare namespace svg="http://www.w3.org/2000/svg";
declare namespace xlink="http://www.w3.org/1999/xlink";
declare namespace b="http://mansoft.nl/xqib";

declare variable $leveldoc external;
declare variable $levelnr external;
declare variable $event external;

declare function chain:process-field($row, $column, $field ) {
  let $x := ($column - 1) * 10 + 1
      , $y := ($row  - 1) * 10 + 1
  return
    if (empty($field)) then
      <use xmlns="http://www.w3.org/2000/svg" xlink:href="#empty" x="{$x}" y="{$y}"/>
    else
      <use xmlns="http://www.w3.org/2000/svg" xlink:href="{concat('#', $field/@shape)}" x="{$x}" y="{$y}" fill="{$field/@color}" color="{if ($field/@state = 'current') then 'black' else if ($field/@state = 'visited') then 'white' else $field/@color}" chain:row="{$row}" chain:column="{$column}"/>
};

declare %updating function chain:load-svg() {
    let $chainreaction := $leveldoc/chain:chain-game
        , $chain-field := $chainreaction/chain:field
        , $columns := xs:integer($chainreaction/@columns)
        , $rows := xs:integer($chainreaction/@rows)
		, $svg := b:dom()/svg:svg
    return
    (
        replace value of node $svg/@viewBox with concat("0 0 ", $columns * 10 + 2, " ", $rows * 10 + 2),
        replace node b:dom()/svg:svg/svg:title/text() with concat("Chainreaction level ", $levelnr),
        (
        for $row in 1 to $rows, $column in 1 to $columns
            let
                $field := $chain-field[xs:integer(@row) = $row][xs:integer(@column) = $column]
                , $use := chain:process-field($row, $column, $field)
            return insert node $use as last into $svg
        )
    )
};

declare %updating function chain:click-svg($use) {
    if (not(empty($use/@chain:row)) and not(empty($use/@chain:column))) then
    (
        let $field := $leveldoc/chain:chain-game/chain:field
            , $unvisited := $field[@state = 'unvisited']
            , $new-field := $field[@row = $use/@chain:row][@column = $use/@chain:column]
            , $current-field := $field[@state = 'current']
            , $current-row := data($current-field/@row)
            , $current-col := data($current-field/@column)
            , $new-row := data($new-field/@row)
            , $new-col := data($new-field/@column)
            , $current-svg-color := $use/../svg:use[@chain:row = $current-row][@chain:column = $current-col]/@color
            , $new-svg-color := $use/@color
        return
        (
            if (data($new-field/@state) = 'current') then 
                b:alert('Same position')
            else if ($new-row != $current-row and $new-col != $current-col) then
                b:alert('Bad move: Stay in row or column')
            else if ($new-field/@state = 'visited') then
                b:alert('Bad move: Already been there')
            else if ($new-field/@shape != $current-field/@shape and $new-field/@color != $current-field/@color) then
                b:alert('Bad move: Must match colour or shape')
            else
            (
                if (count($unvisited) = 1) then b:alert('Solved!') else (),
                replace value of node $current-field/@state with 'visited',
                replace value of node $new-field/@state with 'current',
                replace value of node $current-svg-color with 'white',
                replace value of node $new-svg-color with 'black'
            )
        )
    )
    else
        ()
};

declare %updating function chain:add-listeners() {
	b:addEventListener(b:dom()//svg:svg, "click", "chain:click-svg(.)")
};
	
]]>
	</script>
	<defs>
		<rect id="empty" width="10" height="10" stroke="lightgrey" fill="white" stroke-width="0.2"/>
		<g id="circle" stroke-width="0.5">
			<use xlink:href="#empty"/>
			<circle r="3.8" cx="5" cy="5"/>
			<line x1="3" y1="3" x2="7" y2="7" stroke="currentColor"/>
			<line x1="3" y1="7" x2="7" y2="3" stroke="currentColor"/>
		</g>
		<g id="square" stroke-width="0.5">
			<use xlink:href="#empty"/>
			<rect x="1.5" y="1.5" width="7" height="7"/>
			<line x1="3" y1="3" x2="7" y2="7" stroke="currentColor"/>
			<line x1="3" y1="7" x2="7" y2="3" stroke="currentColor"/>
		</g>
	</defs>
</svg>
